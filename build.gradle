apply plugin: 'java'

sourceCompatibility = 1.8
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            srcDir file('src/it/java')
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources.srcDir file('src/it/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    testCompile 'junit:junit:4.12'
    integrationTestCompile 'org.assertj:assertj-core:3.2.0'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }  // Always run IT tests
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

// vi: filetype=groovy
