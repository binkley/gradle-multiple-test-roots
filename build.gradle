// TODO: How to use "plugins {" with Spring Boot?
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE'
    }
}

plugins {
    id 'application'
    id 'java'
    id 'org.unbroken-dome.test-sets' version '1.2.0'
}
apply plugin: 'spring-boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

testSets {
    integrationTest { dirName = 'it' }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    testCompile 'junit:junit:4.12'
    integrationTestCompile 'org.assertj:assertj-core:3.2.0'
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
project.integrationTest {
    outputs.upToDateWhen { false } // Always run IT
}

// Work around issue #12 in test sets plugin
tasks.withType(Test) {
    reports.html.destination = new File(project.reportsDir, name)
    reports.junitXml.destination  \
        = new File(project.buildDir, "${name}-results")
    binResultsDir = new File(reports.junitXml.destination, "binary")
}

// vi: filetype=groovy
